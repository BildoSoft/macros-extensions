<job id="main">
	<script language="VBScript">
   
    Dim WshShell, BtnCode
    Set WshShell = WScript.CreateObject("WScript.Shell")
    
    Dim oberonApp, oberonServerWasNotRunning
    Call ConnectToServer("Oberon.Document", oberonApp, oberonServerWasNotRunning)
    
    Set WshShell = WScript.CreateObject("WScript.Shell")

    If Not oberonApp Is Nothing Then
        Dim result 

        Set gridRow = CreateObject("Oberon.GridRow")
        gridRow.RowNo =  oberonApp.GetGridRowCount - 1
        Set gridCells = oberonApp.GetGridItems(gridRow)
        For Each gridCell In gridCells
            result = result & "(" & gridCell.RowNo & "," & gridCell.ColNo & ") " & gridCell.Value & vbCr
        Next
        WshShell.Popup result, 10, "Row N°" &  gridRow.RowNo & ":", 0

        Set gridColumn = CreateObject("Oberon.GridColumn")
        gridColumn.ColNo = oberonApp.GetGridColumnCount - 1
        Set gridCells = oberonApp.GetGridItems(gridColumn)
        result = ""
        For Each gridCell In gridCells
            result = result & "(" & gridCell.RowNo & "," & gridCell.ColNo & ") " & gridCell.Value & vbCr
        Next
        WshShell.Popup result, 10, "Column N°" &  gridColumn.ColNo & ":", 0
        
   End If

	Sub ConnectToServer(progId, server, serverWasNotRunning)
		' Test pour déterminer si une copie du Server est déjà en exécution.
		On Error Resume Next    ' Retarde la récupération d'erreur.
		' La fonction Getobject appelée sans le premier argument renvoie une référence à une occurrence de
		' l'application. Si l'application n'est pas en exécution, une erreur se produit. Notez la virgule
		' utilisée en tant que premier espace réservé d'argument.
		Set server = GetObject(, progId)
		If Err.Number <> 0 Then serverWasNotRunning = True
		Err.Clear   ' Efface l'objet Err si une erreur s'est produite.
		If serverWasNotRunning = True Then Set server = CreateObject(progId)
	End Sub
	</script>
</job>